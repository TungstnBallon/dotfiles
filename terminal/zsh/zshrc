# Environment Variables
export PATH="$HOME/.local/bin/:$HOME/.bin:$HOME/.cargo/bin:$HOME/.local/share/npm/bin:$PATH"
[[ ! -v EDITOR ]] && export EDITOR='nvim'
export MANPAGER="sh -c 'sed -u -e \"s/\\x1B\[[0-9;]*m//g; s/.\\x08//g\" | bat -p -lman'"
export npm_config_prefix="$HOME/.local/share/npm"
export VIRTUAL_ENV_DISABLE_PROMPT=1

# History
HISTSIZE=5000
HISTFILE=~/.zsh_history
SAVEHIST=$HISTSIZE
HISTDUP=erase
setopt appendhistory
setopt sharehistory
setopt hist_ignore_space
setopt hist_ignore_all_dups
setopt hist_save_no_dups
setopt hist_ignore_dups
setopt hist_find_no_dups

# Prompt
zmodload zsh/parameter  # Needed to access jobstates variable
setopt promptsubst
PS1='$(prompt jobs="${#jobstates}" laststatus="$?" shell=zsh)$ '

# Aliases
alias ls='ls --color --classify'
alias tree='tree -C -F'
alias te='$EDITOR'
alias vim="nvim --noplugin"
alias diffed='$EDITOR $(git diff --name-only --relative)'
alias pacdiff='DIFFPROG="nvim -d" pacdiff -b -3 -s'

# Load completions
autoload -U compinit && compinit

#### FZF SETUP ####
export FZF_DEFAULT_OPTS="--ansi --color=bw,hl:2,spinner:3 --style=minimal"
fzf_excludes=("--exclude=Steam" "--exclude=.steam" "--exclude=Games" "--exclude=.wine" "--exclude=.cache" "--exclude=.git")
export FZF_DEFAULT_COMMAND="fd --hidden --type l --type f --type d $fzf_excludes"

# Shell integrations
source <(fzf --zsh)

function fzf-homedir-widget() {
    setopt localoptions pipefail no_aliases 2> /dev/null
    local dir="$(fd --type d --hidden --maxdepth 8 --follow $fzf_excludes . ~ | fzf)"
    if [[ -z "$dir" ]]; then
      zle redisplay
      return 0
    fi
    zle push-line # Clear buffer. Auto-restored on next prompt.
    BUFFER="builtin cd -- ${(q)dir:a}"
    zle accept-line
    local ret=$?
    unset dir # ensure this doesn't end up appearing in prompt expansion
    zle reset-prompt
    return $ret
}
zle -N fzf-homedir-widget
bindkey '^F' fzf-homedir-widget

source "/usr/share/zsh/plugins/fzf-tab-git/fzf-tab.zsh"
# Force zsh not to show completion menu, which allows fzf-tab to capture the unambiguous prefix
zstyle ':completion:*' menu no
# To make fzf-tab follow FZF_DEFAULT_OPTS.
# NOTE: This may lead to unexpected behavior since some flags break this plugin. See Aloxaf/fzf-tab#455.
zstyle ':fzf-tab:*' use-fzf-default-opts yes

