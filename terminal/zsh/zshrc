# Environment Variables
export PATH="$HOME/.local/bin/:$HOME/.bin:$HOME/.cargo/bin:$HOME/.local/share/npm/bin:$PATH"
[[ ! -v EDITOR ]] && export EDITOR='nvim'
export MANPAGER="sh -c 'sed -u -e \"s/\\x1B\[[0-9;]*m//g; s/.\\x08//g\" | bat -p -lman'"
export npm_config_prefix="$HOME/.local/share/npm"
export VIRTUAL_ENV_DISABLE_PROMPT=1

# History
HISTSIZE=5000
HISTFILE=~/.zsh_history
SAVEHIST=$HISTSIZE
HISTDUP=erase
setopt appendhistory
setopt sharehistory
setopt hist_ignore_space
setopt hist_ignore_all_dups
setopt hist_save_no_dups
setopt hist_ignore_dups
setopt hist_find_no_dups

# Prompt
zmodload zsh/parameter  # Needed to access jobstates variable
setopt promptsubst
PS1='$(prompt jobs="${#jobstates}" laststatus="$?" shell=zsh)'

# Aliases
alias ls='ls --color --classify'
alias tree='tree -C -F'
alias te='$EDITOR'
alias vim="nvim --noplugin"
alias diffed='$EDITOR $(git diff --name-only --relative)'
alias pacdiff='DIFFPROG="nvim -d" pacdiff -b -3 -s'

# Load completions
autoload -U compinit && compinit

#### SKIM ####
export SKIM_DEFAULT_OPTIONS="--ansi --color=bw"
source "/usr/share/skim/key-bindings.zsh"
function skim-homedir-widget() {
	fd_excludes=("--exclude=Steam" "--exclude=.steam" "--exclude=Games" "--exclude=.wine" "--exclude=.cache" "--exclude=.git")
	cmd="fd --type d --hidden --maxdepth 8 --follow ${fd_excludes[*]} . ~"
	setopt localoptions pipefail no_aliases 2> /dev/null
	dir="$(eval "$cmd" | sk --no-multi --tiebreak=length)"
	if [[ -z "$dir" ]]; then
		zle redisplay
		return 0
	fi
	if [[ -z "$BUFFER" ]]; then
		BUFFER="cd ${(q)dir}"
		zle accept-line
	else
		print -sr "cd ${(q)dir}"
		cd "$dir"
	fi
	local ret=$?
	unset dir
	unset cmd
	unset fd_excludes
	zle skim-redraw-prompt
	tput cnorm
	return $ret
}
zle -N skim-homedir-widget
bindkey '^F' skim-homedir-widget

#### FZF-TAB ####
export FZF_DEFAULT_OPTS="$SKIM_DEFAULT_OPTIONS --style=minimal"
source "/usr/share/zsh/plugins/fzf-tab-git/fzf-tab.zsh"
# Force zsh not to show completion menu, which allows fzf-tab to capture the unambiguous prefix
zstyle ':completion:*' menu no
# To make fzf-tab follow FZF_DEFAULT_OPTS.
# NOTE: This may lead to unexpected behavior since some flags break this plugin. See Aloxaf/fzf-tab#455.
zstyle ':fzf-tab:*' use-fzf-default-opts yes

